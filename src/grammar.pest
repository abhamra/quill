//! This is the set of rules for Quill's parser
Program = _{ SOI ~ (Stmt ~ NEWLINE*)* ~ EOI }

Stmt = _{ AssignStmt 
        | GateStmt 
        | MeasureStmt 
        | ReturnStmt 
        | COMMENT }

// Assignment Statement
AssignStmt = { RespectExpr ~ " " ~ "create " ~ 
"oo " ~ Type ~ " " ~ Name ~ " with value " ~ Value }
RespectExpr = { "Maistow" | "Canstow" }

// Gate Application Statement
GateStmt = { MultiControlGateStmt | DoubleParamGateStmt | DoubleGateStmt | SingleParamGateStmt | SingleGateStmt }

// Single Gate Application
SingleGateStmt = _{ "Thy " ~ Q1Gate ~ " shalt target " ~ QubitTarget }
Q1Gate = { "h"
		 | "x" 
         | "y"
         | "z"
         | "sadj"
         | "s"
         | "tadj"
         | "t" }

// Single Parameterized Gate Application
SingleParamGateStmt = _{ "Thy " ~ Q1ParamGate ~ " shalt target " ~ QubitTarget ~ " with " ~ ValList }
Q1ParamGate = { "rx"
			  | "ry"
              | "rz" 
              | "u3" }

// Double Gate Application
DoubleGateStmt = _{ "Thy " ~ Q2Gate ~ " shalt target " ~ QubitTarget ~ " and control on " ~ ControlList }
Q2Gate = { "cnot"
		 | "swap" 
         | "cx"
         | "cz" } 

// Double Parameterized Gate Application
DoubleParamGateStmt = _{ "Thy " ~ Q2ParamGate ~ " shalt target " ~ QubitTarget ~ " with " ~ ValList }
Q2ParamGate = { "rxx"
              | "ryy" 
              | "rzz" }


// Multi-Controlled Gate Application
MultiControlGateStmt = _{ "Thy " ~ QMultiGate ~ " shalt target " ~ (QubitTarget | QRegSlice ) ~ " and control on " ~ ControlList }

QMultiGate = { "mcx" 
	     | "mcy"
             | "mcz"
             | ("toffoli" | "ccx") }

// Measurement (from qubit to a classical bit) Statement
MeasureStmt = { "Rede " ~ QubitTarget ~ " and quyken " ~ CBitTarget }

// Return Statement
ReturnStmt = { "Return " ~ Int }

// All currently supported types in Quill
Type = {  "qubit" 
		| "qreg" 
        | "cbit" 
        | "creg" }


// Rules for all values
/// A value can hold any of these types

// Idea: Value should be silenced, unsilence all children

Value = _{ QRegTensor | QReg | CReg | Qubit | CBit | PI }
ValList = { "[" ~ ((Value | Float | Int) ~ ", "?)+ ~ "]" }
ControlList = { (QubitTarget ~ ", "?)+ }

// A keyword to support PI, which we want as an inherent feature of
// our language. We support PI, PI[i], and PI[i, j], where the latter
// two statements mean PI * i and PI * (i / j) respectively
PI = { "PI" ~ ("[" ~ (Index ~ ", "?){,2} ~ "]")? }

// These "target" rules aim to simplify the process of working with 
// either a name (a variable) or a qreg / creg slice (an individual
// qubit or cbit)
QubitTarget = _{ (QRegSlice | Name) }
CBitTarget = _{ (CRegSlice | Name) }

// The QReg value, largely for instantiation purposes. The QRegSlice is
// more commonly used for accessing individual qubits or a range of qubits
// for gate applications
QReg = { (Qubit ~ "[" ~ (Index) ~ "]") }
QRegTensor = { QReg ~ (" + " ~ QReg)+ }
QRegSlice = { (Name ~ "[" ~ Index ~ ".." ~ Index ~ "]") | (Name ~ "[" ~ Index ~ "]") }

// The Index is meant to target non-negative values, and is a separate rule
// largely because it isn't silenced
Index = { (("0" ~ !ASCII_DIGIT) | (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)) }

// The Qubit Type
Qubit = { ("0" | "1" | "+" | "-") ~ !Value }

// Integers
Int = { "-"? ~ (("0" ~ !ASCII_DIGIT) | (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)) } // how do we handle 05, for example?

// Floats
Float = { "-"? ~ (("0" ~ !ASCII_DIGIT) | (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)) ~ "." ~ ASCII_DIGIT+ }

// Classical Bit (CBit) -> O or 1
CBit = { ("b0" | "b1") ~ !CBit }

// Classical Bit Register (similar to QReg implementation). CRegSlice
// is for accessing single (or ranges of) bit(s), similar to QRegSlice
CReg = { (CBit ~ "[" ~ (Int | Name) ~ "]") ~ (" + " ~ CReg)? }
CRegTensor = { QReg ~ (" + " ~ QReg)+ }
CRegSlice = { (Name ~ "[" ~ Index ~ ".." ~ Index ~ "]") | (Name ~ "[" ~ Index ~ "]") }

// Name is used to match all variables after all other rules (such as QRegSlices)
// have been exhausted. Variables cannot start with a number, but are otherwise
// given alphanumeric freedom
Name = { ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }

// Comment (self-explanatory)
COMMENT = { "//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE+ }

